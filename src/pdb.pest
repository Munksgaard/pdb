WHITESPACE = _{ " " | "\n" }

keyword = { "let" | "insert" | "select" | "from" | "into" | "create" | "end" | "lambda" | "in" | "case" | "of" | "type" }

identifier = @{ !keyword ~ ('a'..'z' ~ ASCII_ALPHANUMERIC*) }

tyident = @{ 'A'..'Z' ~ ASCII_ALPHANUMERIC* }

longtyident = { tyident ~ tyterm+ }

unit = { "()" }

tytuple = { "(" ~ ty ~ ("," ~ ty)+ ~ ","? ~ ")" }

tyrecord = { "{" ~ identifier ~ ":" ~ ty ~ ("," ~ identifier ~ ":" ~ ty)* ~ ","? ~ "}" }

tyfun = { tyterm ~ "->" ~ ty }

tyterm = { unit | tytuple | tyrecord | identifier | tyident | "(" ~ ty ~ ")" }

tycon = { tyident ~ tyterm* }

datatype = { "type" ~ tyident ~ identifier* ~ "=" ~
             "|"? ~ tycon ~
             ("|" ~ tycon)*
}

ty = { longtyident | tyfun | tyterm }

int = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
}

bool = { "True" | "False" }

tuple = { "(" ~ expr ~ ("," ~ expr)+ ~ ","? ~ ")" }

string = ${ "\"" ~ inner ~ "\"" }

inner = @{ char* }

char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

record = { "{" ~ identifier ~ "=" ~ expr ~ ("," ~ identifier ~ "=" ~ expr)* ~ ","? ~ "}" }

letbind = { ( "let" ~ identifier ~ "=" ~ expr )+ ~ "in" ~ expr ~ "end" }

lambda = { "lambda" ~ identifier ~ "->" ~ expr }

tuple_pat = { "(" ~ pattern ~ ("," ~ pattern)+ ~ ","? ~ ")" }

record_pat = { "{" ~ identifier ~ "=" ~ pattern ~ ("," ~ identifier ~ "=" ~ pattern)* ~ ","? ~ "}" }

wildcard = { "_" }

pattern = { wildcard | atom | record_pat | tuple_pat | identifier }

case = { "case" ~ expr ~ "of" ~ "|"? ~ pattern ~ "=>" ~ expr ~ ( "|" ~ pattern ~ "=>" ~ expr)* ~ "end" }

atom = { unit | bool | int | string }

term = { atom | identifier | tyident | letbind | tuple | record | lambda | case | "(" ~ expr ~ ")" }

expr = { term+ }

letdecl = { "let" ~ identifier ~ "=" ~ expr }

insert = { "insert" ~ expr ~ "into" ~ identifier }

select = { "select" ~ "from" ~ identifier }

create = { "create" ~ "table" ~ identifier ~ ty }

statement = _{ SOI ~ (create | insert | select | letdecl | datatype) ~ EOI }
